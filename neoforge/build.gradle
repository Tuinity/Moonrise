import net.fabricmc.loom.util.aw2at.Aw2At

plugins {
    id("xyz.jpenilla.quiet-architectury-loom")
    id 'maven-publish'
    id 'com.gradleup.shadow'
}

repositories {
    maven {
        name = 'NeoForged'
        url = 'https://maven.neoforged.net/releases/'
    }
}

configurations.implementation {
    extendsFrom(configurations.shadow)
}

dependencies {
    add('shadow', project([path: ":", configuration: "namedElements"]))
    neoForge "net.neoforged:neoforge:${rootProject.neoforge_version}"

    shadow("ca.spottedleaf:concurrentutil:${rootProject.concurrentutil_version}")
    shadow("org.yaml:snakeyaml:${rootProject.snakeyaml_version}")
    forgeExtra("org.yaml:snakeyaml:${rootProject.snakeyaml_version}")

    modImplementation "me.shedaniel.cloth:cloth-config-neoforge:${rootProject.cloth_version}"
    include "me.shedaniel.cloth:cloth-config-neoforge:${rootProject.cloth_version}"
}

processResources {
    inputs.property "version", project.version

    filesMatching("META-INF/neoforge.mods.toml") {
        expand "version": project.version, "minecraft_version": minecraft_version, "loader_version": loader_version, "mod_version": mod_version
    }
}

shadowJar {
    archiveClassifier = "dev-all"
    destinationDirectory = layout.buildDirectory.dir("libs")
    configurations = [project.configurations.shadow]
    relocate 'ca.spottedleaf.concurrentutil', 'ca.spottedleaf.moonrise.libs.ca.spottedleaf.concurrentutil'
    relocate 'org.yaml.snakeyaml', 'ca.spottedleaf.moonrise.libs.org.yaml.snakeyaml'
}

Aw2At.setup(getProject(), tasks.remapJar)

publishMods {
    modLoaders = ["neoforge"]

    modrinth {
        incompatible(
            "notenoughcrashes",
            "starlight-neoforge",
            "canary"
        )
    }
    curseforge {
        incompatible(
            "not-enough-crashes-forge",
            "starlight-neoforge",
            "canary"
        )
    }
}
